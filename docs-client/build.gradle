/*
 * Copyright 2018 LINE Corporation
 *
 * LINE Corporation licenses this file to you under the Apache License,
 * version 2.0 (the "License"); you may not use this file except in compliance
 * with the License. You may obtain a copy of the License at:
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations
 * under the License.
 */

plugins {
    id 'com.moowork.node' version '1.2.0'
}

apply plugin: 'base'

task buildWeb(type: YarnTask) {
    dependsOn 'yarn'

    args = ['run', 'build']

    inputs.dir('src')
    inputs.file('package.json')
    inputs.file('yarn.lock')
    outputs.dir('build/web')
}

task checkWeb(type: YarnTask) {
    dependsOn 'yarn'

    args = ['run', 'check']

    inputs.dir('src')
    inputs.file('package.json')
    inputs.file('yarn.lock')
    // Hackily set the output to an unrelated file, this let's the task be
    // up-to-date when the sources don't change even though it has no real
    // outputs.
    outputs.file('tsconfig.json')
}

task copyWeb(type: Copy) {
    dependsOn buildWeb

    from 'build/web'
    into 'build/javaweb/com/linecorp/armeria/server/docs'
}

tasks.assemble.dependsOn copyWeb
tasks.check.dependsOn checkWeb

node {
    version = '8.11.3'
    download = true
}
